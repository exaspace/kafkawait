import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.14.2'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

description = 'Kafka Wait'

repositories {
    mavenCentral()
}

sourceSets {
    performanceTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/performance-test/java')
        }
        resources.srcDir file('src/performance-test/resources')
    }
}

configurations {
    performanceTestCompile.extendsFrom testCompile
    performanceTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.apache.kafka:kafka-clients:3.3.1'
    implementation 'org.slf4j:slf4j-api:2.0.5'

    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    
    testImplementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation 'junit:junit:4.13'
    testImplementation 'com.github.charithe:kafka-junit:4.2.3'
    testImplementation 'org.eclipse.jetty:jetty-server:11.0.12'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'

    performanceTestCompile 'org.apache.httpcomponents:httpasyncclient:4.1.5'
}

scmVersion {
    tag {
        prefix = ""
    }
}

project.version = scmVersion.version

sourceCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jar {
    baseName = "$project.name"
    version = "$project.version"
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task testJar(type: ShadowJar) {
    classifier = 'tests'
    from sourceSets.test.output
    configurations = [project.configurations.testCompileClasspath]
}

task performanceTest(type: Test) {
    testClassesDirs = sourceSets.performanceTest.output.classesDirs
    classpath = sourceSets.performanceTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

task runDemoWebServer(type:JavaExec) {
    main = "org.exaspace.kafkawait.demo.CalculatorWebServer"
    classpath = sourceSets.test.runtimeClasspath
}

task runDemoEventProcessor(type: JavaExec) {
    main = "org.exaspace.kafkawait.demo.CalculatorEventProcessor"
    classpath = sourceSets.test.runtimeClasspath
}

check.dependsOn performanceTest
performanceTest.mustRunAfter test
